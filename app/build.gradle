import com.github.triplet.gradle.androidpublisher.ResolutionStrategy

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.protobuf' version '0.8.18'
    id 'org.jmailen.kotlinter'
    id 'com.github.triplet.play' version '3.7.0'
    id 'com.gladed.androidgitversion' version '0.4.14'
}

android {
    compileSdk 31

    signingConfigs {
        file('keystore.properties').with { propsFile ->
            if (propsFile.canRead()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                config {
                    keyAlias props['keyAlias']
                    keyPassword props['keyPassword']
                    storeFile file(props['storeFile'])
                    storePassword props['storePassword']
                }
            } else {
                print('not signing')
            }
        }
    }

    defaultConfig {
        applicationId "is.tiro.heyra"
        minSdk 28
        targetSdk 31

        versionCode androidGitVersion.code()
        versionName androidGitVersion.name()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        file('keystore.properties').with { propsFile ->
            if (propsFile.canRead()) {
                signingConfig signingConfigs.config
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            proto {
                srcDir "${rootProject.projectDir}/third_party/tiro-speech-core/proto"
                srcDir "${rootProject.projectDir}/third_party/descriptor_workaround"
            }
        }
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:${rootProject.ext.protobufVersion}"
        }
        plugins {
            java {
                artifact = "io.grpc:protoc-gen-grpc-java:${rootProject.ext.grpcVersion}"
            }
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${rootProject.ext.grpcVersion}"
            }
            grpckt {
                artifact = "io.grpc:protoc-gen-grpc-kotlin:${rootProject.ext.grpcKotlinVersion}:jdk7@jar"
            }
        }
        generateProtoTasks {
            all().each { task ->
                task.plugins {
                    java {
                        option 'lite'
                    }
                    grpc {
                        option 'lite'
                    }
                    grpckt {
                        option 'lite'
                    }
                }
            }
        }
    }
    namespace 'is.tiro.heyra'
}

play {
    defaultToAppBundles.set(true)
    resolutionStrategy.set(ResolutionStrategy.AUTO)
}

dependencies {
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.5'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.activity:activity:1.2.0'
    implementation "androidx.activity:activity-ktx:1.2.0"
    implementation 'androidx.fragment:fragment:1.3.4'
    implementation 'androidx.fragment:fragment-ktx:1.3.4'
    implementation 'com.google.guava:guava:29.0-android'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'


    implementation "io.grpc:grpc-okhttp:${rootProject.ext.grpcVersion}"
    implementation "io.grpc:grpc-stub:${rootProject.ext.grpcVersion}"
    implementation "io.grpc:grpc-protobuf-lite:${rootProject.ext.grpcVersion}"
    implementation "io.grpc:grpc-kotlin-stub:${rootProject.ext.grpcKotlinVersion}"
    implementation "com.google.protobuf:protobuf-kotlin-lite:${rootProject.ext.protobufVersion}"
}
